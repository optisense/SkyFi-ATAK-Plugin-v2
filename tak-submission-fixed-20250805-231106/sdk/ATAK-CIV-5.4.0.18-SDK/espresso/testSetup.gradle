
ext.jacocoVersion = '0.8.9'

buildscript.dependencies { 
    classpath "org.jacoco:org.jacoco.core:${jacocoVersion}"
}

apply plugin: 'jacoco'

android.buildTypes.debug.testCoverageEnabled (project.hasProperty('coverage'))

configurations {
    androidTestCompileOnly.extendsFrom compileOnly
    testImplementation.extendsFrom compileOnly
}

repositories { 
    flatDir {
        dirs "$rootProject.projectDir/espresso"
    }
}

android { 
    // In order for the test harness to run on the Samsung S9 and possibly others, add in the compilation options
    compileOptions {
         sourceCompatibility JavaVersion.VERSION_1_8
         targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // WARNING: when including dependencies, take care they are not already included in the ATAK apk. Dependencies included in the instrumentation APK and the ATAK APK may cause errors. In my case it caused the Activity::onStart command to hang because I included appcompat and support in the instrumentation APK. The solution for this is to use CompileOnly


    // If the plugin test harness is available as a submodule, load that. Otherwise we assume
    // the plugin is loading the dependency from Maven itself.
    if (new File("$rootProject.projectDir/atakplugintests").exists()) {
        androidTestImplementation project(":atakplugintests")
        testImplementation project(":atakplugintests")
        println("using the submodule version of atakplugintests")
    } else {

        println("found local expresso testing library")
        androidTestImplementation(name:'atakplugintests-debug', ext:'aar')

        androidTestImplementation 'androidx.lifecycle:lifecycle-runtime:2.6.1'
        androidTestImplementation 'androidx.lifecycle:lifecycle-process:2.6.1'

        androidTestImplementation 'androidx.test:orchestrator:1.5.1'
        androidTestImplementation 'androidx.test:runner:1.6.2' 
        androidTestImplementation 'androidx.test:rules:1.6.1'
        androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.3.0'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
        androidTestImplementation 'androidx.test.espresso:espresso-intents:3.6.1'
        androidTestImplementation 'androidx.test.ext:junit:1.2.1'
           
        androidTestImplementation "org.mockito:mockito-core:5.15.2"
        
        androidTestImplementation 'androidx.appcompat:appcompat:1.7.0'
    }

    // This is an easy way to give androidTest and test access to the ATAK main.jar without needing
    // to copy and paste the standard block that checks each possible location a bunch more times.
    // This could backfire if there are other things plugins include as compileOnly that shouldn't
    // be included this way? If so we should switch to a modified block from ATAKVersions.gradle.
    configurations {
        androidTestCompileOnly.extendsFrom compileOnly
        testImplementation.extendsFrom androidTestImplementation

    }

}

android {
    defaultConfig {
        // NoFinishAndroidJUnitRunner ensures that the ATAK activity is not killed, that is its only difference from AndroidJUnitRunner
        testInstrumentationRunner "com.atakmap.android.test.helpers.NoFinishAndroidJUnitRunner"
        // ATAKStarter starts ATAK and sets it up to work well with automated tests
        testInstrumentationRunnerArguments 'listener': 'com.atakmap.android.test.helpers.ATAKStarter'
    }
}

// modApkTask.gradle contains the functions needed for instrumented tests to work
// These functions take the skew name, for example modApk("Debug") will attach its tasks to the connectedDebugAndroidTest task
// modApkTask.gradle also contains tasks that automatically take screenshots on test failure and attach them to the test report
if (new File("$rootProject.projectDir/atakplugintests").exists()) {
    apply from: "$rootProject.projectDir/atakplugintests/modApkTask.gradle"
    println("using the submodule version of the atakplugintests gradle plugin")
} 
else {
    apply from: "$rootProject.projectDir/espresso/modApkTask.gradle"
    println("using a local version of the atakplugintests gradle plugin")
}



// Test Harness using jacoco
// Note:
//    When compiled in debug mode, jacoco inserts itself into the
//    the ATAKApplication class even in the case where the test
//    instrumentation is not run.    This does not seem to be the
//    case with the release builds.
//    The source code for jacoco is not under positive control and
//
//    can be read more about here: https://www.eclemma.org/jacoco/
//    Exception observed on startup -
//    System.err: java.io.FileNotFoundException: /jacoco.exec (Read-only file system)
//        --- snip ---
//       at org.jacoco.agent.rt.internal_8ff85ea.Offline.getProbes(Offline.java:51)
//       at com.atakmap.app.ATAKApplication.$jacocoInit(Unknown Source:13)
//        --- snip ---
//
////////////////////////////////////////////////////////////////////////////////

jacoco {
    toolVersion = "$jacocoVersion"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

android.applicationVariants.all { variant ->
    task "create${variant.name.capitalize()}JacocoTestReport" (type: JacocoReport, dependsOn: ["test${variant.name.capitalize()}UnitTest", "create${variant.name.capitalize()}CoverageReport"])  {

       reports {
            xml.required = true
            html.required = true
        }

        def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', 'androidx/**/*.*' , 'kotlin/**/*.*' ]
        def debugTree = fileTree(dir:"${project.projectDir}/build/intermediates/javac/${variant.name}/classes", excludes: fileFilter)
        def mainSrc = "$project.projectDir/src/main/java"

        sourceDirectories.setFrom(files([mainSrc]))
        classDirectories.setFrom(files([debugTree]))

        executionData.setFrom(fileTree(dir: project.buildDir, includes: [
                "outputs/unit_test_code_coverage/${variant.name}UnitTest/test${variant.name.capitalize()}UnitTest.exec",
                "outputs/code_coverage/${variant.name}AndroidTest/connected/**/*.ec"
        ]))

        description = "Generates unified code coverage report for " + variant.name + " using Jacoco"
    }
}
